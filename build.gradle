plugins {
    id 'org.xtext.xtend' version '2.0.1'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.jfrog.bintray' version '1.7.3'
}

subprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url  "http://dl.bintray.com/franzbecker/maven" 
        }
    }
    
    apply plugin: 'java'
    apply plugin: 'org.xtext.xtend'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    
    group = 'org.testeditor.web'

    ext.versions = [
        dropwizard: '1.3.16',
        xtext: '2.18.0'
    ]

    sourceCompatibility = '1.10'
    targetCompatibility = '1.10'
    
    apply from: "$rootDir/gradle/publishing.gradle"

	sourceSets {
  		main.xtendOutputDir = 'src/main/xtend-gen'
  		test.xtendOutputDir = 'src/test/xtend-gen'
	}

    configurations.all {
	    exclude group: 'asm'
        resolutionStrategy {
            // necessary for xtext 15 that has some emf ecore version resolution problems
            force "org.antlr:antlr-runtime:3.2"
            force "org.eclipse.emf:org.eclipse.emf.ecore:$versions.xtext"
        }
    }

    dependencies {

        compile "org.eclipse.xtend:org.eclipse.xtend.lib:$versions.xtext"
        compile "com.google.inject:guice:4.2.+"

        // Workaround for (a problem similiar to this) issue: https://github.com/eclipse/xtext/issues/1231
        // A SecurityException is thrown due to non-matching signer information of different classes in the same package.
        compile 'org.eclipse.platform:org.eclipse.core.runtime:3.15.0'

        // necessary for java 9,10 and probably 11, since some packages have been removed from standard runtime
        compile 'javax.activation:activation:1.1.1'
        compile 'javax.annotation:javax.annotation-api:1.3.2'
        compile 'javax.xml.bind:jaxb-api:2.3.0'
    }

}

release {
    preTagCommitMessage = '[release]'
    tagCommitMessage = '[release]'
    newVersionCommitMessage = '[release] new version'
    tagTemplate = 'v${version}'
}
