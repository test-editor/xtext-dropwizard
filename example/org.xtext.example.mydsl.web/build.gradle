plugins {
	id 'war'
	id 'de.undercouch.download' version '3.2.0'
}
import de.undercouch.gradle.tasks.download.Download

dependencies {
	compile project(':org.xtext.example.mydsl')
	compile project(':org.xtext.example.mydsl.ide')
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase.web:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.web.servlet:${xtextVersion}"
	compile "org.eclipse.xtend:org.eclipse.xtend.lib:${xtextVersion}"
	compile "org.webjars:requirejs:2.3.2"
	compile "org.webjars:jquery:2.2.4"
	providedCompile "org.eclipse.jetty:jetty-annotations:9.3.8.v20160314"
	
	// Dependencies on the org.testeditor.web.dropwizard project
	compile "org.testeditor.web:org.testeditor.web.dropwizard:1.0.0"
}
def orionDir = file('src/main/webapp/orion')
def orionUrl = 'http://download.eclipse.org/orion/drops/R-11.0-201602232219/built-codeEdit.zip'

task downloadOrion(type: Download) {
	src orionUrl
	dest file("$buildDir/orion/built-codeEdit.zip")
}

task unpackOrion(dependsOn: downloadOrion, type: Copy) {
	from(zipTree(downloadOrion.dest))
	into(orionDir)
}

task jettyRun(type:JavaExec) {
	dependsOn(sourceSets.main.runtimeClasspath, unpackOrion)
	classpath = sourceSets.main.runtimeClasspath.filter{it.exists()}
	main = 'org.xtext.example.mydsl.web.ServerLauncher'
	standardInput = System.in
	group = 'run'
	description = 'Starts an example Jetty server with your language'
}
